ext {
  componentName='Interlok Storage/Elastic: Common'
  componentDesc="Elasticsearch components that are not transport specific"
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
  elasticVersion = '7.17.19'
  jacksonVersion = '2.17.0'
}

dependencies {
  api ("com.adaptris:interlok-json:$interlokCoreVersion") { changing=true}
  api ("com.adaptris:interlok-csv:$interlokCoreVersion") { changing=true}
  // INTERLOK-3559 since we removed apache-csv out of interlok-csv it's
  // a bigger deal to remove it out of elastic
  api("org.apache.commons:commons-csv:1.10.0")
  api("net.sf.supercsv:super-csv:2.4.0")

  implementation ("org.elasticsearch:elasticsearch:$elasticVersion") {
    exclude group: "org.yaml", module: "snakeyaml"
    exclude group: "org.apache.logging.log4j", module:"log4j-api"
  }
  implementation ("org.apache.lucene:lucene-core:9.10.0")
  implementation ("org.elasticsearch:elasticsearch-x-content:$elasticVersion")
  // Dependency to platform/BOM
  implementation (platform("com.fasterxml.jackson:jackson-bom:$jacksonVersion"))
  implementation ("com.fasterxml.jackson.core:jackson-databind")
  implementation ("com.fasterxml.jackson.dataformat:jackson-dataformat-smile")
  implementation ("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml")
  implementation ("com.fasterxml.jackson.dataformat:jackson-dataformat-cbor")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.extensions.delombokTask
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
              "Build-Jdk": System.getProperty('java.version'),
              "Implementation-Title": componentName,
              "Implementation-Version": project.version,
              "Implementation-Vendor-Id": project.group,
              "Implementation-Vendor": organizationName)
  }
}

publishing {
  publications {
      mavenJava(MavenPublication) {
          from components.java

          artifact javadocJar { classifier "javadoc" }
          artifact sourcesJar { classifier "sources" }
          pom.withXml {
            asNode().appendNode("name", componentName)
            asNode().appendNode("description", componentDesc)
			asNode().appendNode("url", "https://interlok.adaptris.net/interlok-docs/#/")
            def properties = asNode().appendNode("properties")
            properties.appendNode("externalUrl", "https://www.elastic.co/products/elasticsearch")
            properties.appendNode("target", "3.9.1+")
            properties.appendNode("license", "false")
            properties.appendNode("tags", "elastic,elasticsearch")
            properties.appendNode("readme", "https://github.com/adaptris/interlok-elastic/raw/develop/README.md")
            properties.appendNode("repository", "https://github.com/adaptris/interlok-elastic")
          }
      }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}

delombok {
  target = delombokTargetDir
}

task deleteGeneratedFiles(type: Delete) {
  delete delombokTargetDir
}

clean.dependsOn deleteGeneratedFiles
