ext {
  componentName='Interlok Storage/Elastic: REST Client'
  componentDesc="Producing to Elasticsearch using their REST api"
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
  elasticVersion = '7.15.1'
}

dependencies {
  api project(':interlok-elastic-common')
  implementation ("org.elasticsearch.client:elasticsearch-rest-high-level-client:$elasticVersion") {
    exclude group: "org.yaml", module: "snakeyaml"
    exclude group: "org.apache.logging.log4j", module:"log4j-api"
  }
  implementation ("org.elasticsearch.client:elasticsearch-rest-client-sniffer:$elasticVersion") {
    exclude group: "commons-codec", module: "commons-codec"
    exclude group: "org.apache.httpcomponents", module: "httpclient"
    exclude group: "org.apache.logging.log4j", module:"log4j-api"
  }
  api ("com.adaptris:interlok-apache-http:$interlokCoreVersion") { changing= true}
  implementation ("commons-codec:commons-codec:1.15")
  implementation ("org.apache.httpcomponents:httpclient:4.5.13")

  // Versions derived from the BOM defined in the parent.
  implementation ("com.fasterxml.jackson.core:jackson-databind")
  implementation ("com.fasterxml.jackson.dataformat:jackson-dataformat-smile")
  implementation ("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml")
  implementation ("com.fasterxml.jackson.dataformat:jackson-dataformat-cbor")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.extensions.delombokTask
}

task examplesJar(type: Jar, dependsOn: test) {
  classifier = 'examples'
  from new File(buildDir, '/examples')
}

artifacts {
    archives javadocJar
    archives sourcesJar
    archives examplesJar
}

jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
              "Build-Jdk": System.getProperty('java.version'),
              "Implementation-Title": componentName,
              "Implementation-Version": project.version,
              "Implementation-Vendor-Id": project.group,
              "Implementation-Vendor": organizationName)
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact javadocJar { classifier "javadoc" }
      artifact sourcesJar { classifier "sources" }
      artifact examplesJar { classifier "examples" }
      pom.withXml {
        asNode().appendNode("name", componentName)
        asNode().appendNode("description", componentDesc)
        asNode().appendNode("url", "https://interlok.adaptris.net/interlok-docs/#/")
        def properties = asNode().appendNode("properties")
        properties.appendNode("externalUrl", "https://www.elastic.co/products/elasticsearch")
        properties.appendNode("target", "3.9.1+")
        properties.appendNode("license", "false")
        properties.appendNode("tags", "elastic,elasticsearch")
        properties.appendNode("readme", "https://github.com/adaptris/interlok-elastic/raw/develop/README.md")
        properties.appendNode("repository", "https://github.com/adaptris/interlok-elastic")
      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}

delombok {
  target = delombokTargetDir
}

task deleteGeneratedFiles(type: Delete) {
  delete delombokTargetDir
}

clean.dependsOn deleteGeneratedFiles
